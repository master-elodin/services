def scriptProperties = [
    name: "Services",
    version: "0.0.1",
    author: "Tim VanDoren",
    description: "Make the Service Console look nicer!",
    // target is what Tampermonkey uses to decide which pages to run on
    target: "/(swaservices).*(SWAServicesConsole)/",
    externalDependencies: ["https://cdnjs.cloudflare.com/ajax/libs/jquery/3.2.1/jquery.min.js",
        "https://cdnjs.cloudflare.com/ajax/libs/knockout/3.4.0/knockout-min.js",
        "https://cdnjs.cloudflare.com/ajax/libs/knockout.mapping/2.4.1/knockout.mapping.min.js",
        "https://cdnjs.cloudflare.com/ajax/libs/js-cookie/2.1.3/js.cookie.min.js"]
]

def getBinFolder = {
    def binFolder = new File("bin")
    if(!binFolder.exists()) {
        binFolder.mkdirs()
    }
    binFolder
}

def getPlaceholderMatches = { line -> line =~ /\$\{.*\}/ }
def getPlaceholderName = { placeholder -> placeholder.trim()[2..-2]}

def fillProperties = { line, dependencyModifier ->
    getPlaceholderMatches(line).each {
        def propertyName = getPlaceholderName(it)
        def property = scriptProperties[propertyName]
        if(propertyName == "content") {
            property = file("bin/combined.js").text
        } else if(property == null) {
            throw new GradleScriptException("No property found for placeholder: ${it}", null)
        }
        if(propertyName == "externalDependencies") {
            property = dependencyModifier(property)
        }
        logger.info "replacing ${it} with ${property}"
        line = line.replace(it, property)
    }
    line
}

def replaceFromFile = { line, lineModifier={ text -> text } ->
    def lineToWrite = line
    def placeholderMatches = getPlaceholderMatches(line)
    if(placeholderMatches) {
        def placeholder = placeholderMatches[0]
        lineToWrite = line.replace(placeholder, lineModifier(new File("${getPlaceholderName(placeholder)}").text))
    }
    lineToWrite
}

def combineStuff = { fileType, lineModifier={ text -> text } ->
    new File(getBinFolder(), "combined.${fileType}").withWriter { writer ->
        new File("src/shell/shell.${fileType}").eachLine { line ->
            writer << "${replaceFromFile(line, lineModifier)}\n"
        }
    }
}

task combineHtml {
    inputs.dir("src")
    outputs.dir("bin")

    doLast {
        def stripText = { text -> text.replaceAll(/(>\s*\n*<)(\n*)/, '><') }
        combineStuff("html", stripText)
        def combinedHtml = file("bin/combined.html")
        // TODO: figure out how to remove extra whitespace between elements
        def html = combinedHtml.collect { stripText(replaceFromFile(it)) }.join('')
        combinedHtml.write("jQuery('html').html('${html}');");
    }
}

task combineJs {
    inputs.dir("src")
    outputs.dir("bin")
    dependsOn 'combineHtml'

    doLast {
        // add a tab because it looks better for both scripts
        combineStuff("js", { text -> "    ${text.split('\n').join('\n    ')}"})
    }
}

task createDevtoolsScript {
    dependsOn 'combineJs'
    inputs.dir("src")

    doLast {
        new File(getBinFolder(), "${scriptProperties.name.toLowerCase()}-devtools.js").withWriter { writer ->
            new File("src/templates/devtools.js").eachLine { line ->
                writer << fillProperties("${line}\n", { property -> "\"${property.collect { it }.join("\",\"")}\"" })
            }
        }
    }
}

task createTampermonkeyScript {
    dependsOn 'combineJs'
    inputs.dir("src")

    doLast {
        new File(getBinFolder(), "${scriptProperties.name}.user.js").withWriter { writer ->
            new File("src/templates/tampermonkey.js").eachLine { line ->
                writer << fillProperties("${line}\n", { property -> property.collect { "// @require      ${it}"}.join("\n") })
            }
        }
    }
}

task clean << {
    delete("bin")
}

tasks.create('build').dependsOn 'createTampermonkeyScript','createDevtoolsScript'
